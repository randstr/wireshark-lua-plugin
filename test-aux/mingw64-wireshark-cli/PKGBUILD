# Maintainer: Wireshark Core Team <wireshark-dev@wireshark.org>

_realname=wireshark
pkgbase=mingw-w64-${_realname}-cli
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-cli")
pkgver=4.1.0rc0.r2892.g69a530c030
pkgrel=1
pkgdesc="Network traffic and protocol analyzer/sniffer (CLI tools)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://www.wireshark.org"
license=('GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-c-ares"
         "${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-libgcrypt"
         "${MINGW_PACKAGE_PREFIX}-pcre2"
         "${MINGW_PACKAGE_PREFIX}-speexdsp"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-python"
             'git')
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest"
              "${MINGW_PACKAGE_PREFIX}-python-pytest-xdist")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_commit="69a530c03073923a2d60a6069ee9ba5c078af465"
source=("${_realname}"::"git+https://github.com/wireshark/wireshark.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "${_realname}"

  git describe --long "${_commit}" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_wireshark=OFF \
      -DBUILD_rawshark=OFF \
      -DBUILD_dumpcap=OFF \
      -DBUILD_text2pcap=OFF \
      -DBUILD_mergecap=OFF \
      -DBUILD_reordercap=OFF \
      -DBUILD_editcap=OFF \
      -DBUILD_capinfos=OFF \
      -DBUILD_captype=OFF \
      -DBUILD_randpkt=OFF \
      -DBUILD_dftest=OFF \
      -DBUILD_dcerpcidl2wrs=OFF \
      -DBUILD_androiddump=OFF \
      -DBUILD_sshdump=OFF \
      -DBUILD_ciscodump=OFF \
      -DBUILD_dpauxmon=OFF \
      -DBUILD_ranpktdump=OFF \
      -DBUILD_wifidump=OFF \
      -DBUILD_udpdump=OFF \
      -DBUILD_sharkd=OFF \
      -DBUILD_mmdbresolve=OFF \
      -DENABLE_WERROR=OFF \
      -DENABLE_WINSPARKLE=OFF \
      -DFETCH_lua=ON \
      ../${_realname}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build . --target test-programs
  "${MINGW_PREFIX}"/bin/pytest.exe -k suite_unittest
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install . --component Development

  install -Dm644 "${srcdir}/${_realname}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
